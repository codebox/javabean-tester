group = "net.codebox"
version = "1.0.0"

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'junit', name: 'junit', version: '4.11'

    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
}

signing {
    sign configurations.archives
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'JavaBeanTester'
                packaging 'jar'
                artifactId 'javabean-tester'
                description 'Library for automatically unit-testing Java Bean classes using reflection'
                url 'https://github.com/codebox/javabean-tester'

                scm {
                    connection 'scm:git@github.com/codebox/javabean-tester.git'
                    developerConnection 'scm:git@github.com/codebox/javabean-tester.git'
                    url 'https://github.com/codebox/javabean-tester'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'codebox'
                        name 'Rob Dawson'
                        email 'rob@codebox.net'
                    }
                }
            }
        }
    }
}